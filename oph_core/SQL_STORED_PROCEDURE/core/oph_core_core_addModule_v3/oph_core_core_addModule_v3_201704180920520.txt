
CREATE procedure [core].[addModule_v3] @accountid nvarchar(50), @db nvarchar(50), @masterDB varchar(50), @isdebug bit=0
--WITH ENCRYPTION
as
	declare @accountguid uniqueidentifier, @accountdbGUID uniqueidentifier, @curMaster bit 

	select @accountguid=a.AccountGUID, @accountdbGUID=AccountDBGUID, 
		@curMaster=isMaster
	from acct a
		inner join acctdbse b
			on a.AccountGUID=b.AccountGUID
	where accountid=@accountid and b.DatabaseName=@db
	
	declare @sqlstr nvarchar(max), @sqlpar nvarchar(max)

	--for ismaster=1
	-- ismaster=1: semua modl yang digunakan adalah dari database tersebut
	set @sqlstr='
	insert into '+@masterDB+'.dbo.modl (ModuleGUID, AccountGUID, ModuleID, ModuleDescription, settingMode, AccountDBGUID, ParentModuleGUID, OrderNo, needLogin, ThemePageGUID)
	select newid(), '''+cast(@accountguid as varchar(50))+''', a.tablename, null, case upper(left(a.tablename, 1)) when ''C'' then 0 when ''M'' then 1 when ''P'' then 2 when ''S'' then 3 when ''T'' then 4 when ''Y'' then 5 else 9 end, '''+cast(@accountdbGUID as varchar(50))+''', null, null, 1, null
	from '+@db+'.gen.cotabl a
		left join '+@masterDB+'.dbo.modl m on a.tablename=m.ModuleID
	where m.ModuleGUID is null'

	if @isdebug=1 print @sqlstr
	exec sp_executesql @sqlstr
	 

	set @sqlstr='
		insert into '+@masterDB+'.dbo.modlcolm (ColumnGUID, ModuleGUID, colKey, colType, colOrder, titlecaption)
		select newid(), a.moduleguid, a.colname, a.system_type_id, a.colorder, a.titlecaption
		from (	
			select m.moduleguid, m.ModuleID, b.colname, b.colorder, y.system_type_id, b.titlecaption
			from '+@masterDB+'.dbo.modl m
				inner join '+@db+'.sys.objects x on m.moduleid=x.name
				inner join '+@db+'.sys.columns y on x.object_id=y.object_id
				inner join '+@db+'.gen.cotabl a on m.moduleid=a.tablename
				inner join '+@db+'.gen.cotablfiel b on a.tableguid=b.tableguid and y.name=b.colname
			) a 
		
			left join (
				select c.ModuleID, d.colKey 
				from '+@masterDB+'.dbo.modl c 
					inner join '+@masterDB+'.dbo.modlcolm d on c.ModuleGUID=d.moduleguid
				) e
				on e.colkey=a.colname and e.moduleid=a.ModuleID
		where e.moduleid is null'
	
	if @isdebug=1 print @sqlstr
	exec sp_executesql @sqlstr

	set @sqlstr='
		select a.name, a.name  
		from '+@db+'.sys.columns a inner join '+@db+'.sys.objects b on a.object_id=b.object_id
		where b.name=''cotablfiel''
			and a.name not in (''TableFieldGUID'', ''TableGUID'', ''ColName'', ''id'', ''ColOrder'', ''xType'', ''DeletedDate'',
			''CreatedDate'', ''UpdatedDate'', ''DeletedUser'', ''CreatedUser'', ''UpdatedUser'', ''TriggerDate'', ''TriggerDate2'')'
	
	declare @infoTable api.fieldList

	if @isdebug=1 print @sqlstr

	insert into @infoTable (fieldid, fieldvalue)	
	exec sp_executesql @sqlstr

	--select * from @infoTable

	DECLARE #cur1 CURSOR
	READ_ONLY
	FOR 
		select fieldid from @infoTable

	DECLARE @infoKey varchar(40)
	OPEN #cur1

	FETCH NEXT FROM #cur1 INTO @infoKey
	WHILE (@@fetch_status <> -1)
	BEGIN
		IF (@@fetch_status <> -2)
		BEGIN
			set @sqlstr='
				insert into '+@masterDB+'.dbo.modlcolminfo (ColumnInfoGUID, ColumnGUID, InfoKey, InfoValue)
				select newid(), x.ColumnGUID, '''+@infokey+''', infovalue
				from (
					select c.ModuleGUID, d.ColumnGUID, a.'+@infokey+' infovalue
					from '+@db+'.gen.cotablfiel a
						inner join '+@db+'.gen.cotabl b on a.TableGUID=b.TableGUID
						inner join '+@masterDB+'.dbo.modl c on c.ModuleID=b.TableName
						inner join '+@masterDB+'.dbo.modlcolm d on c.ModuleGUID=d.moduleguid and a.colname=d.colKey
					) x
		
						left join (
							select a.moduleguid, b.columnguid
							from '+@masterDB+'.dbo.modl a 
								inner join '+@masterDB+'.dbo.modlcolm b on a.ModuleGUID=b.moduleguid 
								inner join '+@masterDB+'.dbo.modlcolminfo c on b.ColumnGUID=c.ColumnGUID
							where c.InfoKey='''+@infokey+''') y

							on x.moduleguid=y.moduleguid and x.columnguid=y.columnguid
				where y.moduleguid is null
				'

			if @isdebug=1 print @sqlstr

			exec sp_executesql @sqlstr


		END
		FETCH NEXT FROM #cur1 INTO @infoKey
	END

	CLOSE #cur1
	DEALLOCATE #cur1

	delete modlcolminfo where InfoValue is null
	


