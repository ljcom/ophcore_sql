
CREATE procedure [gen].[uploadChild] @hostGUID uniqueidentifier, @code nvarchar(50), @GUID uniqueidentifier, @filepath nvarchar(max), @isdebug bit=0
as
	declare @ver varchar(10), @db varchar(50), @typecode varchar(1)
	exec gen.getDBInfo @hostguid, @code, @ver output, @db output, @typecode output

	declare @sqlstr nvarchar(max), @typename varchar(10)

	declare @ranGUID nvarchar(50)=replace(cast(newid() as nvarchar(50)), '-', '')

	if gen.fn_checkFile(@filepath)=1
	begin
		set @sqlstr=
			'
			select * 
			into ##t'+@ranGUID+'
			FROM OPENROWSET(
				''Microsoft.ace.OLEDB.12.0'',
				''Excel 8.0;Database='+@filepath+';HDR=YES'',
				''SELECT * FROM ['+@code+'$]'')
				
			--select * from ##t'+@ranGUID+'
			
				'
		if @isdebug=1 print @sqlstr	
		exec sp_executesql @sqlstr

		declare @fieldKey nvarchar(max), @fieldCast nvarchar(max), @fieldOnly nvarchar(max)

		select @fieldKey=b.colkey 
		from modl a
			inner join modlcolm b on a.ModuleGUID=b.ModuleGUID
		where moduleid=@code and b.colOrder=(select min(colorder) from modlcolm where moduleguid=a.ModuleGUID)
		
		set @fieldCast=stuff((
			select ', cast('+b.name+' as nvarchar(max)) '+b.name 
			from tempdb.sys.objects a
				inner join tempdb.sys.columns b
					on a.object_id=b.object_id 
			where a.name like '##t'+@ranGUID+'%' and b.name<>@fieldkey
			for xml path('')), 1, 2, '')

		set @fieldOnly=stuff((
			select ', '+b.name
			from tempdb.sys.objects a
				inner join tempdb.sys.columns b
					on a.object_id=b.object_id 
			where a.name like '##t'+@ranGUID+'%' and b.name<>@fieldkey
			for xml path('')), 1, 2, '')
			print @fieldCast
			print @fieldOnly

		set @sqlstr='			
			declare @hostGUID uniqueidentifier='''+cast(@hostGUID as varchar(50))+'''

			DECLARE #cur1 CURSOR
			READ_ONLY
			FOR		
				select '+@fieldkey+' from ##t'+@ranGUID+'

			OPEN #cur1
			declare @GUID uniqueidentifier, @saveList nvarchar(max)
			FETCH NEXT FROM #cur1 INTO @GUID

			WHILE (@@fetch_status <> -1)
			BEGIN
				IF (@@fetch_status <> -2)
				BEGIN
					set @saveList=(
						select ''<field id="''+infokey+''"><value>''+infovalue+''</value></field>''
						from (
							select '+@fieldkey+', '+@fieldCast+'
							--select *
							from ##t'+@ranGUID+' where '+@fieldKey+'=@GUID) a
						unpivot(
							infovalue 
							for infokey in ('+@fieldOnly+')
							) as x
						for xml path(''''))

					
					set @saveList=''<sqroot>''+replace(replace(@savelist, ''&lt;'', ''<''), ''&gt;'', ''>'')+''</sqroot>''
					print @hostGUID
					print @GUID
					print @savelist
					exec api.[save] @hostGUID, '''+@code+''', @GUID, @saveList

				END
				FETCH NEXT FROM #cur1 INTO @GUID
			END

			CLOSE #cur1
			DEALLOCATE #cur1
			'

		if @isdebug=1 print @sqlstr	
		exec sp_executesql @sqlstr

	end
	else
		print 'file not found: '+@filepath



